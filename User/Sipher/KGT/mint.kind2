//Test Function to add money
#kdl_name = KgtMint
User.Sipher.KGT.mint
  (miner: U120)
  (current_block: U120)
  (state: KDL.KGT.State)
: KDL.KGT.State
User.Sipher.KGT.mint miner current_block (KDL.KGT.State.new last_mint balances) =

// Constants 
// -----------------------------------------------------------------------------
  // Total blocks per halving
  let bk_base = 125000000u120

  // Xp Per Halving Base Starts with "21000000000000000000" (21 Quintillion)
  let xp_base = 21000000000000000000u120
// -----------------------------------------------------------------------------

  // Halving number is increased by 1 every 125000000 (125 Million) blocks
  let halving = U120.div last_mint bk_base

  // No experience can be obtained once halving reaches 38
  // This function becomes useless on block 64
  if (U120.greater_equal halving 64u120) {
    KDL.KGT.State.new last_mint balances
  } else { 
    let xp            = User.Sipher.KGT.xp xp_base bk_base current_block last_mint
    let new_value     = (x: U120) => U120.add xp x
    let new_balance   = User.Sipher.PatriciaTree.mut balances miner 0u120 new_value
    KDL.KGT.State.new current_block new_balance
  }


// mine_block    is the block that the person wants to mint
// last_mint     is the last time that xp was minted

// x = current_block
// y = last_mint
User.Sipher.KGT.xp
  (xp_base         : U120)
  (bk_base         : U120)
  (current_block   : U120)
  (last_mint       : U120)
: U120
User.Sipher.KGT.xp xp_base  bk_base current_block last_mint = 
  if (U120.greater_equal last_mint current_block) { 
    0u120
  } else {
    let halving = U120.div last_mint bk_base
    let value   = User.Sipher.KGT.XP_Per_Block xp_base halving bk_base
    U120.add value (User.Sipher.KGT.xp xp_base bk_base current_block (U120.inc last_mint))
  }


// Returns the XP that we receive per Halving
User.Sipher.KGT.XP_Per_Halving
  (xp_base         : U120)
  (current_halving : U120)
: U120 {
  U120.div xp_base (U120.shift_left 1u120 current_halving)
}


// XP that can be obtained per block
User.Sipher.KGT.XP_Per_Block
  (xp_base         : U120)
  (current_halving : U120)
  (bk_base         : U120)
: U120 { 
  U120.div (User.Sipher.KGT.XP_Per_Halving xp_base current_halving) bk_base
}
